//
// Standard over-approximating SLL predicate with contents
//
predicate SLL(+x, vs) {
  // Empty SLL
  (x == null) * (vs == []);
  // One SLL node and the rest
  (x -b> #v, #next) * SLL(#next, #vs) *
  (vs == #v :: #vs)
}

//
// Pure predicate for list membership
//
predicate list_member(+vs, +v, r : Bool){
  (vs == []) * (r == false);
  (vs == v :: #rest) * (r == true) * list_member(#rest, v, #mem);
  (vs == #v :: #rest) * (! (#v == v)) * list_member(#rest, v, r)
}

//
// Lemma: List membership append
//
lemma list_member_append {
  statement:
    forall vs, v, r, w.
      list_member(vs, v, r) |- list_member(vs @ [ w ], v, (r || (w = v)))

  proof:
    if (w = v) {} else {}; // FIXME: THIS IS HORRIFIC
    unfold list_member(vs, v, r);
    if (not (vs = [])) {
      assert {bind: #nv, #nvs, #nr} (vs == #nv :: #nvs) * list_member(#nvs, #v, #nr);
      apply list_member_append(#nvs, v, #nr, w)
    }
}

//
// Lemma: List membership concat
//
lemma list_member_concat {
  statement:
    forall vs1, vs2, v.
      list_member(vs1, v, #r1) * list_member(vs2, v, #r2) |- list_member(vs1 @ vs2, v, (#r1 || #r2))

  proof:
    unfold list_member(vs1, v, #r1);
    if (not (vs1 = [])) {
      assert {bind: #nv1, #nvs1, #nr1} (vs1 == #nv1 :: #nvs1) * list_member(#nvs1, v, #nr1);
      apply list_member_concat(#nvs1, vs2, v)
    }
}

// 00. Allocating an SLL node with the given value
{ v == #v }
function SLL_allocate_node(v){
  t := new(2);
  [t] := v;
  [[ fold SLL(null, []) ]];
  [[ fold SLL(t, [ #v ]) ]];
  return t
}
{ SLL(ret, [ #v ]) }


//
// RECURSIVE SLL MANIPULATION
//

// 01. Prepending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #vs) }
function SLL_prepend(x, k){
    z := SLL_allocate_node(k);
    [[ unfold SLL(z, k) ]];
    [z + 1] := x;
    [[ fold SLL(z, (k :: #vs)) ]];
    return z
}
{ SLL(ret, #k :: #vs) }

// 02. Appending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #vs) }
function SLL_append(x, k){
  [[ unfold SLL(x, #vs) ]];
  if (x = null) {
    x := SLL_allocate_node(k)
  } else {
    t := [x + 1];
    z := SLL_append(t, k);
    [x + 1] := z;
    [[ fold SLL(x, #vs @ [k]) ]]
  };
  return x
}
{ SLL(ret,  #vs @ [#k]) }

// 03. Appending a given SLL node to a given SLL
{ (x == #x) * (y == #y) * SLL(#x, #vs) * SLL(#y, [#vy]) }
function SLL_append_node(x, y) {
  [[ unfold SLL(x, #vs) ]];
  if (x = null) {
    x := y
  } else {
    t := [x + 1];
    z := SLL_append_node(t, y);
    [x + 1] := z;
    [[ fold SLL(x, #vs @ [#vy])]]
  };
  return x
}
{ SLL(ret, #vs @ [#vy]) }

// 04. Concatenating two lists
{(x == #x) * (y == #y) * SLL(#x, #vx) * SLL(#y, #vy) }
function SLL_concat(x, y) {
  [[ unfold SLL(x, #vx) ]];
  if (x = null){
    x := y
  } else {
    t := [x + 1];
    z := SLL_concat(t, y);
    [x + 1] := z;
    [[ fold SLL(x, #vx @ #vy) ]]
  };
  return x
}
{ SLL(ret,  #vx @ #vy) }

// 05. Copying a given SLL
{ (x == #x) * SLL(#x, #vs) }
function SLL_copy(x){
  y := null;
  [[ unfold SLL(x, #vs) ]];
  if (not (x = null)) {
    k := [x];
    y := SLL_allocate_node(k);
    [[ unfold SLL(y, [ k ]) ]];
    t := [x + 1];
    z := SLL_copy(t);
    [y + 1] := z
  } else {
    skip
  };
  [[ fold SLL(x, #vs) ]];
  [[ fold SLL(y, #vs) ]];
  return y
}
{ SLL(#x, #vs) * SLL(ret, #vs) }

// 06. Calculating the length of a given SLL
{ (x == #x) * SLL(#x, #vs) }
function SLL_length(x) {
  n := 0;
  [[  unfold SLL(x, #vs) ]];
  if (x = null) {
    n := 0
  } else {
    t := [x + 1];
    n := SLL_length(t);
    n := 1 + n
  };
  [[ fold SLL(x, #vs) ]];
  return n
}
{ SLL(#x, #vs) * ret == len(#vs) }

// 07. Reversing a given SLL
{ (x == #x) *  SLL(#x, #vs) }
function SLL_reverse(x){
  [[ unfold SLL(x, #vs) ]];
  if (not (x = null)) {
    t := [x + 1];
    [x + 1] := null;
    [[ assert {bind: #v, #beta} (#vs == (#v :: #beta)) ]];
    z := SLL_reverse(t);
    [[ fold SLL(null, [] )]];
    [[ fold SLL(x, [ #v ])]];
    y := SLL_append_node(z, x)
  } else {
    y := null;
    [[ fold SLL(y, rev(#vs)) ]]
  };
  return y
}
{ SLL(ret,rev #vs) }

// 08. Checking if a given value is in a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #vs) * list_member(#vs, #k, #r) }
function SLL_member(x, k){
  found := false;
  [[ unfold SLL(x, #vs) ]];
  [[ unfold list_member(#vs, k, #r) ]];
  if (x = null){
    skip
  } else {
    v := [x];
    if (v = k){
      found := true
    } else {
      t := [x + 1];
      found := SLL_member(t, k)
    }
  };
  [[ fold list_member(#vs, k, #r) ]];
  [[ fold SLL(x, #vs) ]];
  return found
}
{ SLL(#x, #vs) * list_member(#vs, #k, #r) * (ret == #r) }

// 09. Removing a given value from a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #vs) * list_member(#vs, #k, #mem) }
function SLL_remove(x, k) {
  [[ unfold SLL(x, #vs) ]];
  [[ unfold list_member(#vs, k, #mem) ]];
  if (x = null) {
    [[ fold SLL(x, #vs) ]]
  } else {
    v := [x];
    next := [x + 1];
    if (v = k){
      free(x);
      x := SLL_remove(next, k)
    } else {
       z := SLL_remove(next, k);
       [x + 1] := z;
       [[ assert {bind: #zs} SLL(z, #zs) ]];
       [[ fold SLL(x, (v :: #zs)) ]];
       [[ fold list_member((v :: #zs), #k, false) ]]
    }
  };
  [[ fold list_member([], #k, false) ]];
  return x
}
{ SLL(ret, #nvs) * list_member(#nvs, #k, false) }

// 10. Freeing a given SLL
{ (x == #x) * SLL(#x, #vs) }
function SLL_free(x){
  [[ unfold SLL(x, #vs) ]];
  if (x = null) {
    skip
  } else {
    t := [x + 1];
    z := SLL_free(t);
    free(x)
  };
  return null
}
{ (ret == null) }

//
// ITERATIVE LIST MANIPULATION
//

//
// Standard over-approximating SLL segment predicate with contents
//
predicate SLLseg(+x, y, vs) {
  (x == y) * (vs == []);
  (x -b> #v, #next) * SLLseg(#next, y, #vs) * (vs == #v :: #vs)
}

//
// Lemma: appending a given value to a given SLLseg
//
lemma SSLseg_append {
  statement:
    forall x, vs, v, z.
      SLLseg(x, #y, vs) * (#y -b> v, z) |- SLLseg(x, z, vs @ [ v ])

  proof:
    unfold SLLseg(x, #y, vs);
    if (not (x = #y)) {
      assert {bind: #nv, #nnext, #nvs}
        (x -b> #nv, #nnext) * SLLseg(#nnext, #y, #nvs) *
        (vs == #nv :: #nvs);
      apply SSLseg_append(#nnext, #nvs, v, z);
      fold SLLseg(x, z, vs @ [ v ])
    } else {
      fold SLLseg(#y, z, [ v ])
    }
}

//
// Lemma: an SLLseg followed by an SLL is an SLL
//
lemma SLLseg_concat_SLL {
  statement:
    forall x, y.
      SLLseg(x, y, #vx) * SLL(y, #vy) |- SLL (x, #vx @ #vy)

  proof:
    unfold SLLseg(x, y, #vx);
    if (not (#vx = [])){
      assert {bind: #nv, #nnext} (x -b> #nv, #nnext);
      apply SLLseg_concat_SLL(#nnext, y)
    }
}

//
// Lemma: a null-terminating SLLseg is an SLL
//
lemma SLLseg_to_SLL {
  statement:
    forall x.
      SLLseg(x, null, #vx) |- SLL(x, #vx)

  proof:
    unfold SLLseg(x, null, #vx);
    if (not (#vx = [])){
      assert {bind: #nv, #nnext} (x -b> #nv, #nnext);
      apply SLLseg_to_SLL(#nnext)
    }
}

// 02. Appending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #vx) }
function SLL_append_iter(x, k){
  [[ unfold SLL(x, #vx) ]];
  y := SLL_allocate_node(k);
  if (x = null) {
    x := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ fold SLLseg(head, prev, [])]];
    [[ invariant {bind: prev, next, #vs1, #vs2, #v}
          SLLseg(head, prev, #vs1) * (prev -b> #v, next) * SLL(next,  #vs2) *
           (#vx == #vs1 @ (#v :: #vs2)) ]];
    while(not (next = null)){
      [[ assert {bind: #prev} prev == #prev ]];
      [[ unfold SLL(next, #vs2) ]];
      prev := next;
      next := [next + 1];
      [[ apply SSLseg_append(head,  #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind: #svs, #sv} SLLseg(head, prev, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, #svs, #sv, y) ]];
    [[ assert (SLLseg(head, y, #vx)) ]];
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return x
}
{ SLL(ret, #vx @ [ #k ]) }

// 03. Appending a given node to a given SLL
{ (x == #x) * (y == #y) * SLL(#x, #vs) * SLL(#y, [#vy]) }
function SLL_append_node_iter(x, y){
  [[ unfold SLL(x, #vs) ]];
  if (x = null) {
    x := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ fold SLLseg(head, prev, []) ]];
    [[ invariant {bind: prev, next, #vs1, #vs2, #v}
          SLLseg(head, prev, #vs1) * (prev -b> #v, next) * SLL(next,  #vs2) *
           (#vx == #vs1 @ (#v :: #vs2)) ]];
    while(not (next = null)){
      [[ assert {bind: #prev} prev == #prev ]];
      [[ unfold SLL(next, #vs2) ]];
      prev := next;
      next := [next + 1];
      [[ apply SSLseg_append(head,  #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind: #svs, #sv} SLLseg(head, prev, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, #svs, #sv, y) ]];
    [[ assert (SLLseg(head, y, #vs)) ]]; // <-- add deliberate bug to #vx
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return x
}
{ SLL(ret, #vs @ [#vy]) }

// 04. Concatenating two lists
{(x == #x) * (y == #y) * SLL(#x, #vx) * SLL(#y, #vy) }
function SLL_concat_iter(x, y){
  [[ unfold SLL(x, #vx) ]];
  if (x = null) {
    head := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ fold SLLseg(head, prev, []) ]];
    [[ invariant {bind: prev, next, #vs1, #vs2, #v}
          SLLseg(head, prev, #vs1) * (prev -b> #v, next) * SLL(next,  #vs2) *
           (#vx == #vs1 @ (#v :: #vs2)) ]];
    while (not (next = null)) {
        [[ assert {bind: #prev} prev == #prev ]];
        [[ unfold SLL(next, #vs2) ]];
        prev := next;
        next := [next + 1];
        [[ apply SSLseg_append(head,  #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind:#svs, #sv} SLLseg(head, prev, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, #svs, #sv, y) ]];
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return head
}
{ SLL(ret, #vx @ #vy) }

// 05. Copying a given SLL
{ (x == #x) * SLL(#x, #alpha) }
function SLL_copy_iter(x){
    [[ unfold SLL(x, #alpha) ]];
    y := null;
    if (x = null){
      [[ fold SLL(y, #alpha) ]];
      [[ fold SLL(x, #alpha) ]]
    } else {
        y := new(2);
        v := [x];
        [y] := v;
        t := [x + 1];
        p := y;
        [[ fold SLLseg(t, t, []) ]];
        [[ fold SLLseg(x, t, [v]) ]];
        [[ fold SLLseg(y, p, []) ]];
        [[ invariant {bind: c, v, t, p, #alpha1, #alpha2, #alpha3, #a}
          SLLseg(x, t, #alpha1) * SLL(t, #alpha2) * (v == #a) *
            (#alpha == (#alpha1 @ #alpha2)) * SLLseg(y, p, #alpha3) * (p -b> #a, null) *
                (#alpha1 == (#alpha3 @ [#a])) ]];
        while (not (t = null)){
            [[ assert {bind: #t, #p} (t == #t) * (p == #p) ]];
            [[ unfold SLL(t, #alpha2) ]];
            v := [t];
            c := new(2);
            [c] := v;
            [p + 1] := c;
            p := c;
            t := [t + 1];
            [[ apply SSLseg_append(x, #alpha1, v, t) ]];
            [[ apply SSLseg_append(y, #alpha3, #a, p) ]]
        };
        [[ assert {bind: #alpha3} SLLseg(y, p, #alpha3) ]];
        [[ apply SSLseg_append(y, #alpha3, v, null) ]];
        [[ apply SLLseg_to_SLL(x) ]];
        [[ apply SLLseg_to_SLL(y) ]]
    };
    return y
}
{ SLL(#x, #alpha) * SLL(ret, #alpha) }

// 06. Calculating the length of a given SLL
{ (x == #x) * SLL(x, #vx) }
function SLL_length_iter(x) {
  y := x;
  n := 0;
  [[ fold SLLseg(x, y, []) ]];
  [[invariant {bind: n, y, #nvx,  #nvy}
      SLLseg(x, y,  #nvx) * SLL(y, #nvy) *
         (#vx == (#nvx@#nvy)) * (n == len #nvx) ]];
  while (not (y = null)) {
    [[ assert {bind: #y} y == #y ]];
    [[ unfold SLL(y, #nvy) ]];
    [[ assert {bind: #v, #z} #y -b> #v, #z ]];
    y := [y+1];
    n := n+1;
    [[ apply SSLseg_append(x, #nvx, #v, y) ]]
  };
  [[ fold SLL(null, [])]];
  [[ unfold SLL(null, #nvy)]];
  [[ apply SLLseg_to_SLL(x) ]];
  return n
}
{ SLL(#x, #vx) * (ret == len(#vx)) }

// 07. Reversing a given SLL
// { (x == #x) * SLL(#x, #vx) }
// function SLL_reverse_iter(x) {
//    y := null;
//    [[ invariant {bind: x, y, z, #nvx, #nvy}
//        SLL(x, #nvx) * SLL(y, #nvy) * (#vx == ((rev #nvy) @ #nvx)) ]];
//    while (not (x = null)) {
//        z := [x + 1];
//        [x + 1] := y;
//        y := x;
//        x := z
//    };
//    return y
//}
//{ SLL(ret, rev #vx) }

// 08. Checking if a given value is in a given SLL
{ (x == #x) * (k == #k) * SLL(#x, #alpha) * list_member(#alpha, #k, #r) }
function SLL_member_iter(x, k) {
  found := false;
  next := x;
  [[ fold SLLseg(x, next, [])]];
  [[ fold list_member([], k, false) ]];
  [[ invariant {bind: found, next, #beta, #gamma, #rg}
       SLLseg(x, next, #beta) * SLL(next, #gamma) *
         (#alpha == (#beta @ #gamma)) * (#r == (found || #rg)) *
           list_member(#beta, k, found) * list_member(#gamma, k, #rg) ]];
  while ((found = false) && (not (next = null))){
    [[ assert found == false ]];
    [[ assert {bind: #next} next == #next ]];
    [[ unfold SLL(next, #gamma) ]];
    [[ assert {bind: #v, #z} #next -b> #v, #z ]];
    v := [next];
    found := (v = k);
    next := [next + 1];
    [[ apply SSLseg_append(x, #beta, #v, next) ]];
    [[ unfold list_member(#gamma, k, #rg) ]];
    [[ apply list_member_append(#beta, k, false, #v) ]]
  };
  [[ if (found = false) { apply SLLseg_to_SLL(#x); unfold list_member([], #k, false) } else {
      assert {bind: #beta, #gamma} SLLseg(#x, next, #beta) * SLL(next, #gamma);
      apply list_member_concat(#beta, #gamma, #k);
      apply SLLseg_concat_SLL(#x, next)
     } ]];
  return found
}
{ SLL(#x, #alpha) * list_member(#alpha, #k, #r) * (ret == #r) }

// 10. Freeing a given SLL
{ (x == #x) * SLL(#x, #vs) }
function SLL_free_iter(x) {
  [[ invariant {bind: x, #rvs} SLL(x, #rvs) ]];
  while (not (x = null)) {
    [[ unfold SLL(x, #rvs) ]];
    y := x;
    x := [x + 1];
    free(y)
  };
  return null
}
{ (ret == null) }
