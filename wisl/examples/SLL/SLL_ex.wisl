//
// Exact SLL predicate with pointers and contents
//
predicate SLL_ex(+x, ps, vs) {
  // Empty SLL
  (x == null) * (ps == []) * (vs == []);
  // One SLL node and the rest
  (x -b> #v, #next) * SLL_ex(#next, #ps, #vs) *
  (ps == x :: #ps) * (vs == #v :: #vs)
}

//
// Exact freed list predicate with pointers
//
predicate SLL_freed(+ptrs) {
  // Empty freed list
  (ptrs == []);
  // One pointer location and the rest
  (ptrs == #l :: #nptrs) * freed(#l) * SLL_freed(#nptrs)
}

//
// Pure predicate for list membership
//
predicate list_member(+vs, +v, r : Bool){
  (vs == []) * (r == false);
  (vs == v :: #rest) * (r == true) * list_member(#rest, v, #mem);
  (vs == #v :: #rest) * (! (#v == v)) * list_member(#rest, v, r)
}

//
// Lemma: List membership append
//
lemma list_member_append {
  statement:
    forall vs, v, r, w.
      list_member(vs, v, r) |- list_member(vs @ [ w ], v, (r || (w = v)))

  proof:
    if (w = v) {} else {}; // FIXME: THIS IS HORRIFIC
    unfold list_member(vs, v, r);
    if (not (vs = [])) {
      assert {bind: #nv, #nvs, #nr} (vs == #nv :: #nvs) * list_member(#nvs, #v, #nr);
      apply list_member_append(#nvs, v, #nr, w)
    }
}

//
// Lemma: List membership concat
//
lemma list_member_concat {
  statement:
    forall vs1, vs2, v.
      list_member(vs1, v, #r1) * list_member(vs2, v, #r2) |- list_member(vs1 @ vs2, v, (#r1 || #r2))

  proof:
    unfold list_member(vs1, v, #r1);
    if (not (vs1 = [])) {
      assert {bind: #nv1, #nvs1, #nr1} (vs1 == #nv1 :: #nvs1) * list_member(#nvs1, v, #nr1);
      apply list_member_concat(#nvs1, vs2, v)
    }
}

// 00. Allocating an SLL node with the given value
{ v == #v }
function SLL_allocate_node(v){
  t := new(2);
  [t] := v;
  return t
}
{ SLL_ex(ret, [ #t ], [ #v ]) }


//
// RECURSIVE SLL MANIPULATION
//

// 01. Prepending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #ps, #vs) }
function SLL_prepend(x, k){
    z := SLL_allocate_node(k);
    [z + 1] := x;
    return z
}
{ SLL_ex(ret, #z :: #ps, #k :: #vs) }

// 02. Appending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #ps, #vs) }
function SLL_append(x, k){
  if (x = null) {
    x := SLL_allocate_node(k)
    } else {
      t := [x + 1];
      z := SLL_append(t, k);
      [x + 1] := z
  };
  return x
}
{ SLL_ex(ret,  #ps @ [#z], #vs @ [#k]) }

// 03. Appending a given SLL node to a given SLL
{ (x == #x) * (y == #y) * SLL_ex(#x, #px, #vx) * SLL_ex(#y, [#py] ,[#vy]) }
function SLL_append_node(x, y) {
  if (x = null) {
    x := y
  } else {
    t := [x + 1];
    z := SLL_append_node(t, y);
    [x + 1] := z
  };
  return x
}
{ SLL_ex(ret, #px @ [#py], #vx @ [#vy]) }

// 04. Concatenating two lists
{(x == #x) * (y == #y) * SLL_ex(#x, #px, #vx) * SLL_ex(#y, #py ,#vy) }
function SLL_concat(x, y) {
  if (x = null){
    x := y
  } else {
    t := [x + 1];
    z := SLL_concat(t, y);
    [x + 1] := z
  };
  return x
}
{ SLL_ex(ret, #px @ #py, #vx @ #vy) }

// 05. Copying a given SLL
{ (x == #x) * SLL_ex(#x, #ps, #vs) }
function SLL_copy(x){
  y := null;
  if (not (x = null)) {
    k := [x];
    y := SLL_allocate_node(k);
    t := [x + 1];
    z := SLL_copy(t);
    [y + 1] := z
  } else {
    skip
  };
  return y
}
{ SLL_ex(#x, #ps, #vs) * SLL_ex(ret, #nps, #vs) }

// 06. Calculating the length of a given SLL
{ (x == #x) *  SLL_ex(#x, #ps, #vs) }
function SLL_length(x) {
  n := 0;
  if (x = null){
    n := 0
  } else {
    t := [x + 1];
    n := SLL_length(t);
    n := 1 + n
  };
  return n
}
{ ret == len(#vs) }

// 07. Reversing a given SLL
{ (x == #x) *  SLL_ex(#x, #ps, #vs) }
function SLL_reverse(x){
  if (not (x = null)) {
    t := [x + 1];
    [x + 1] := null;
    z := SLL_reverse(t);
    y := SLL_append_node(z, x)
  } else {
    y := null
  };
  return y
}
{ SLL_ex(ret, rev #ps, rev #vs) }

// 08. Checking if a given value is in a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #ps, #vs) * list_member(#vs, #k, #r) }
function SLL_member(x, k){
  found := false;
  if (x = null){
    skip
  } else {
    v := [x];
    if (v = k){
      found := true
    } else {
      t := [x + 1];
      found := SLL_member(t, k)
    }
  };
  return found
}
{ SLL_ex(#x, #ps, #vs) * list_member(#vs, #k, #r) * (ret == #r) }

// 09. Removing a given value from a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #ps, #vs) * list_member(#vs, #k, #mem) }
function SLL_remove(x, k) {
  if (x = null) {
    skip
  } else {
    v := [x];
    next := [x + 1];
    if (v = k){
      free(x);
      x := SLL_remove(next, k)
    } else {
       z := SLL_remove(next, k);
       [x + 1] := z
    }
  };
  return x
}
{ SLL_ex(ret, #nps, #nvs) * list_member(#nvs, #k, false) * SLL_freed(#p) * list_member(#ps, #p, true)}

// 10. Freeing a given SLL
{ (x == #x) * SLL_ex(#x, #ps, #vs) }
function SLL_free(x){
  if (x = null) {
    skip
  } else {
    t := [x + 1];
    z := SLL_free(t);
    free(x)
  };
  return null
}
{ (ret == null) * SLL_freed(#ps)}

//
// ITERATIVE LIST MANIPULATION
//

//
// Standard over-approximating SLL segment predicate with contents
//
predicate SLLseg_ex(+x, +y, ps, vs) {
    (x == y) * (ps == []) * (vs == []);
    (x -b> #v, #next) * SLLseg_ex(#next, y, #ps, #vs) *
    (ps == x :: #ps) * (vs == #v :: #vs)
}

//
// Lemma: SLLseg segment concatenated with an SLL node is an SLLseg
//
lemma SSLseg_append {
  statement:
    forall x, y, ps, vs, v, z.
      SLLseg_ex(x, y, ps, vs) * (y -b> v, z) |- SLLseg_ex(x, z, ps @ [ y ], vs @ [ v ])

  proof:
    unfold SLLseg_ex(x, y, ps, vs);
    if (not (vs = [])) {
      assert {bind: #nv, #nnext, #nps, #nvs}
        (x -b> #nv, #nnext) * SLLseg_ex(#nnext, y, #nps, #nvs) *
        (ps == x :: #nps) * (vs == #nv :: #nvs);
      apply SSLseg_append(#nnext, y, #nps, #nvs, v, z)
    }
}

//
// Lemma: SLLseg concatenated with an SLL is an SLL
//
lemma SLLseg_concat_SLL {
  statement:
    forall x, y.
      SLLseg_ex(x, y, #px, #vx) * SLL_ex(y, #py, #vy) |- SLL_ex (x, #px @ #py, #vx @ #vy)

  proof:
    unfold SLLseg_ex(x, y, #px, #vx);
    if (not (#vx = [])){
      assert {bind: #nv, #nnext} (x -b> #nv, #nnext);
      apply SLLseg_concat_SLL(#nnext, y)
    }
}

//
// Lemma: a null-terminating SLLseg is an SLL
//
lemma SLLseg_to_SLL {
  statement:
    forall x.
      SLLseg_ex(x, null, #px, #vx) |- SLL_ex(x, #px, #vx)

  proof:
    unfold SLLseg_ex(x, null, #px, #vx);
    if (not (#vx = [])){
      assert {bind: #nv, #nnext} (x -b> #nv, #nnext);
      apply SLLseg_to_SLL(#nnext)
    }
}

// 02. Appending a given value to a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #px, #vx) }
function SLL_append_iter(x, k){
  y := SLL_allocate_node(k);
  if (x = null) {
    x := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ invariant {bind: prev, next, #ps1, #ps2, #vs1, #vs2, #v}
          SLLseg_ex(head, prev, #ps1, #vs1) * (prev -b> #v, next) * SLL_ex(next, #ps2, #vs2) *
           (#px == #ps1 @ (prev :: #ps2)) * (#vx == #vs1 @ (#v :: #vs2)) ]];
    while(not (next = null)){
      [[ assert {bind: #prev} prev == #prev ]];
      prev := next;
      next := [next + 1];
      [[ apply SSLseg_append(head, #prev, #ps1, #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind: #sps, #svs, #sv} SLLseg_ex(head, prev, #sps, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, prev, #sps, #svs, #sv, y) ]];
    [[ assert (SLLseg_ex(head, y, #px, #vx)) ]];
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return x
}
{ SLL_ex(ret, #npx, #vx @ [ #k ]) }

// 03. Appending a given node to a given SLL
{ (x == #x) * (y == #y) * SLL_ex(#x, #ps, #vs) * SLL_ex(#y, [#py], [#vy]) }
function SLL_append_node_iter(x, y){
  if (x = null) {
    x := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ invariant {bind: prev, next, #ps1, #ps2, #vs1, #vs2, #v}
          SLLseg_ex(head, prev, #ps1, #vs1) * (prev -b> #v, next) * SLL_ex(next, #ps2, #vs2) *
           (#px == #ps1 @ (prev :: #ps2)) * (#vx == #vs1 @ (#v :: #vs2)) ]];
    while(not (next = null)){
      [[ assert {bind: #prev} prev == #prev ]];
      prev := next;
      next := [next + 1];
      [[ apply SSLseg_append(head, #prev, #ps1, #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind: #sps, #svs, #sv} SLLseg_ex(head, prev, #sps, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, prev, #sps, #svs, #sv, y) ]];
    [[ assert (SLLseg_ex(head, y, #ps, #vs)) ]];
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return x
}
{ SLL_ex(ret, #ps @ [py], #vs @ [#vy]) }

// 04. Concatenating two lists
{(x == #x) * (y == #y) * SLL_ex(#x, #px, #vx) * SLL_ex(#y, #py, #vy) }
function SLL_concat_iter(x, y){
  if (x = null) {
    head := y
  } else {
    head := x;
    prev := head;
    next := [x + 1];
    [[ invariant {bind: prev, next, #ps1, #ps2, #vs1, #vs2, #v}
          SLLseg_ex(head, prev, #ps1, #vs1) * (prev -b> #v, next) * SLL_ex(next, #ps2, #vs2) *
           (#px == #ps1 @ (prev :: #ps2)) * (#vx == #vs1 @ (#v :: #vs2)) ]];
    while (not (next = null)) {
        [[ assert {bind: #prev} prev == #prev ]];
        prev := next;
        next := [next + 1];
        [[ apply SSLseg_append(head, #prev, #ps1, #vs1, #v, prev) ]]
    };
    [prev + 1] := y;
    [[ assert {bind: #sps, #svs, #sv} SLLseg_ex(head, prev, #sps, #svs) * (prev -b> #sv, y) ]];
    [[ apply SSLseg_append(head, prev, #sps, #svs, #sv, y) ]];
    [[ assert (SLLseg_ex(head, y, #ps, #vs)) ]];
    [[ apply SLLseg_concat_SLL(head, y) ]]
  };
  return head
}
{ SLL_ex(ret, #px @ #py, #vx @ #vy) }

// 05. Copying a given SLL
{ (x == #x) * SLL_ex(#x, #px, #alpha) }
function SLL_copy_iter(x){
    y := null;
    if (x = null){
        skip
    } else {
        y := new(2);
        v := [x];
        [y] := v;
        t := [x + 1];
        p := y;
        [[ invariant {bind: c, v, t, p, #alpha1, #alpha2, #alpha3, #px1, #px2, #py, #a, #z}
          SLLseg_ex(x, t, #px1, #alpha1) * SLL_ex(t, #px2, #alpha2) * (v == #a) *
            (#px == (#px1 @ #px2)) * (#alpha == (#alpha1 @ #alpha2)) *
                SLLseg_ex(y, p, #py, #alpha3) * (p -b> #a, null) *
                    (#alpha1 == (#alpha3 @ [#a])) ]];
        while (not (t = null)){
            [[ assert {bind: #t, #p} (t == #t) * (p == #p) ]];
            v := [t];
            c := new(2);
            [c] := v;
            [p + 1] := c;
            p := c;
            t := [t + 1];
            [[ apply SSLseg_append(x, #t, #px1, #alpha1, v, t) ]];
            [[ apply SSLseg_append(y, #p, #py, #alpha3, #a, p) ]]
        };
        [[ assert {bind: #py, #alpha3} SLLseg_ex(y, p, #py, #alpha3) ]];
        [[ apply SSLseg_append(y, p, #py, #alpha3, v, null) ]];
        [[ apply SLLseg_to_SLL(x) ]];
        [[ apply SLLseg_to_SLL(y) ]]
    };
    return y
}
{ SLL_ex(#x, #px, #alpha) * SLL_ex(ret, #nps, #alpha) }

// 06. Calculating the length of a given SLL
{ (x == #x) * SLL_ex(x, #px, #vx) }
function SLL_length_iter(x) {
  y := x;
  n := 0;
  [[invariant {bind: n, y, #nvx, #nvy, #npx, #npy}
      SLLseg_ex(x, y, #npx, #nvx) * SLL_ex(y, #npy, #nvy) *
          (#px == (#npx @ #npy)) * (#vx == (#nvx @ #nvy)) * (n == len #nvx) ]];
  while (not (y = null)) {
    [[ assert {bind: #y} y == #y ]];
    [[ assert {bind: #v, #z} #y -b> #v, #z ]];
    y := [y+1];
    n := n+1;
    [[ apply SSLseg_append(x, #y, #npx,#nvx, #v, y) ]]
  };
  [[ unfold SLL_ex(null, #npy, #nvy)]];
  [[ apply SLLseg_to_SLL(x) ]];
  return n
}
{ SLL_ex(#x, #px, #vx) * (ret == len(#vx)) }

// 07. Reversing a given SLL
{ (x == #x) * SLL_ex(#x, #px, #vx) }
function SLL_reverse_iter(x) {
    y := null;
    [[ invariant {bind: x, y, z, #nvx, #nvy, #npx, #npy}
        SLL_ex(x, #npx, #nvx) * SLL_ex(y, #npy, #nvy) *
            (#px == ((rev #npy) @ #npx)) *(#vx == ((rev #nvy) @ #nvx)) ]];
    while (not (x = null)) {
        z := [x + 1];
        [x + 1] := y;
        y := x;
        x := z
    };
    return y
}
{ SLL_ex(ret, rev #px, rev #vx) }

// 08. Checking if a given value is in a given SLL
{ (x == #x) * (k == #k) * SLL_ex(#x, #ps, #alpha) * list_member(#alpha, #k, #r) }
function SLL_member_iter(x, k) {
  found := false;
  next := x;
  [[ invariant {bind: found, next, #beta, #gamma, #rg, #ps1, #ps2}
       SLLseg_ex(x, next, #ps1, #beta) * SLL_ex(next, #ps2, #gamma) *
            (#ps == (#ps1 @ #ps2)) * (#alpha == (#beta @ #gamma)) *
                (#r == (found || #rg)) * list_member(#beta, k, found) *
                    list_member(#gamma, k, #rg) ]];
  while ((found = false) && (not (next = null))){
    [[ assert found == false ]];
    [[ assert {bind: #next} next == #next ]];
    [[ assert {bind: #v, #z} #next -b> #v, #z ]];
    v := [next];
    found := (v = k);
    next := [next + 1];
    [[ apply SSLseg_append(x, #next, #ps1, #beta, #v, next) ]];
    [[ unfold list_member(#gamma, k, #rg) ]];
    [[ apply list_member_append(#beta, k, false, #v) ]]
  };
  [[ if (found = false) { apply SLLseg_to_SLL(#x); unfold list_member([], #k) } else {
      assert {bind: #beta, #gamma, #ps1, #ps2} SLLseg_ex(#x, next, #ps1, #beta) * SLL_ex(next, #ps2, #gamma);
      apply list_member_concat(#beta, #gamma, #k);
      apply SLLseg_concat_SLL(#x, next)
     } ]];
  return found
}
{ SLL_ex(#x, #ps, #alpha) * list_member(#alpha, #k, #r) * (ret == #r) }

// 10. Freeing a given SLL
{ (x == #x) * SLL_ex(#x, #ps, #vs) }
function SLL_free_iter(x) {
  [[ invariant {bind: x, #rps, #rvs, #fps} SLL_ex(x, #rps, #rvs) * SLL_freed(#fps) * (rev #ps == (rev #fps @ rev #rps))]];
  while (not (x = null)) {
    t := [x + 1];
    free(x);
    x := t
  };
  return null
}
{ (ret == null) * SLL_freed(#ps) }